10 REM PONG
20 REM WRITTEN BY VINCE BRIEL
30 REM MAY 2024 FOR THE C.H.I.P. 
40 REM
60 CLS :LOCATE 22,14:PRINT "PONG!";
70 LOCATE 18,24
80 S=0:H=0 : PAUSE 3000
90 REM START GAME
100 ATTACK 15:FILTER 4:DECAY 19:PLUCK 20
110 CLS :S=0
120 LINE 5,5,94,5,63
130 LINE 94,5,94,74,63
140 LINE 5,5,5,74,63
150 REM SET UP VARIABLES FOR BALL START LOCATION
160 X=50 : Y=73 : P=50
170 REM X&Y ARE BALL LOCATION A&B ARE BALL DIRECTION ON START
180 B=-1: V=RND (10):A=-1: IF V>5 THEN A=1
190 REM PLACE THE BALL & PADDLE ON SCREEN
200 PLOT X,Y,63
210 LINE P-3,74,P+3,74,63
220 REM MAIN ACTION LOOP
225 LOCATE 10,1: PRINT "SCORE = ";S;" HIGH SCORE = ";H
230 IF X+A <6 OR X+A > 93 THEN A=-A : ATTACK 15:FILTER 4:DECAY 12:PLUCK 50
240 IF Y+B < 6 THEN B=-B : ATTACK 15:FILTER 4:DECAY 12:PLUCK 50
250 IF Y+B > 73 THEN GOTO 500
260 PLOT X,Y,0:REM REMOVE BALL TO MOVE
270 X=X+A:Y=Y+B : PLOT X,Y,63
280 IF Y = 73 THEN GOTO 370
290 REM KEYBOARD CHECK FOR MOVE
300 I=INKEY :IF I=0 THEN GOTO 220
310 REM CHECK FOR ARROW MOVE
320 IF I <> 192 THEN GOTO 350
330 IF P < 14 THEN GOTO 220
340 LINE P-3,74,P+3,74,0
342 P = P - 5 
344 GOTO 190
350 IF I <> 193 OR P> 86 THEN GOTO 220
360 LINE P-3,74,P+3,74,0
362 P= P + 5
364 GOTO 190
370 REM CHECK IF PADDLE CONTACTS BALL
380 REM IF IT DOES, REVERSE A AND B DIRECTION!
390 IF X-3=P OR X-2=P THEN GOTO 460
400 IF X-1=P THEN GOTO 460
410 IF X=P THEN GOTO 460
420 IF X+1=P THEN GOTO 460
430 IF X+2=P OR X+3=P THEN GOTO 460
440 REM AT 73 ON SCREEN BUT MISSED PADDLE!
450 GOTO 220
460 REM HIT PADDLE SO BEEP AND REVERSE BALL
470 ATTACK 15:FILTER 4:DECAY 12:PLUCK 50
480 B=-1:S=S+1: IF S > H THEN H=S
490 GOTO 220
500 REM BALL OUT OF BOUNDS!
510 ATTACK 15:FILTER 4:DECAY 19:PLUCK 20
520 PAUSE 500
530 CLS : LOCATE 20,12: PRINT "BALL OUT!"
540 IF S < H THEN GOTO 560
550 LOCATE 16,15:PRINT "NEW HIGH SCORE! ";H;
560 PAUSE 4000
570 GOTO 90
